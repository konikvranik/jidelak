import org.gradle.plugins.ide.eclipse.model.SourceFolder
import org.gradle.plugins.ide.eclipse.model.Container

apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'idea'

version = '0.1'

group = "net.suteren.jidelak" 

mainClassName = "net.suteren.jidelak.tdk.TemplateValidator"

sourceCompatibility = 1.6

repositories { mavenCentral() }

dependencies {
	compile project('jidelak-common')
	compile 'net.sf.jtidy:jtidy:r938'
	compile 'org.slf4j:slf4j-api:1.6.1'
	compile('com.squareup.retrofit:converter-simplexml:1.6.1'){
		exclude module: 'stax'
		exclude module: 'stax-api'
		exclude module: 'xpp3'
	}
	compile 'org.slf4j:slf4j-log4j12:1.7.7'
	compile 'log4j:log4j:1.2.17'
}

configurations { provided }

eclipse.classpath.plusConfigurations += configurations.compile

eclipse {

	classpath {
		file {
			beforeMerged { classpath ->
				classpath.entries.clear()
			}
			whenMerged {  cp ->
				cp.entries.findAll { it instanceof SourceFolder && it.path.startsWith("src/main/") }*.output = "build/classes/main"
				cp.entries.findAll { it instanceof SourceFolder && it.path.startsWith("src/test/") }*.output = "build/classes/test"
				cp.entries.findAll { it instanceof Container && it.path.endsWith("JRE_CONTAINER") }*.exported = false
				cp.entries.findAll { it.kind == "lib" && it.path.contains("slf4j-api") }*.exported = false
				// cp.entries.removeAll { it.kind == "output" }
			}
		}
		defaultOutputDir = file('build/classes/main')
	}

	project {
		natures 'org.springsource.ide.eclipse.gradle.core.nature',
				'org.eclipse.jdt.core.javanature',
				'org.eclipse.jdt.groovy.core.groovyNature'

		buildCommand 'org.eclipse.jdt.core.javabuilder'
		name 'jidelak-validator'
	}
}

jar {
	manifest.
			attributes ( provider: 'gradle',
			'Implementation-Vendor-Id': 'hPa',
			'Implementation-Vendor': 'Petr Vran√≠k',
			'Implementation-Version': 1.0
			)
}

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('DEFAULT_JVM_OPTS=""', 'DEFAULT_JVM_OPTS="-Dhttp.agent=\'Mozilla/5.0 (Linux; U; Android 4.4.4; Nexus 5 Build/KTU84P) AppleWebkit/534.30 (KHTML, like Gecko) Version/4.0 Mobile Safari/534.30\'"')
    }
}
